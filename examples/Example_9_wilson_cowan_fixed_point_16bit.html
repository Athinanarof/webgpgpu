<html>
<script src="../lib/colormaps.js"></script>
<script src="../lib/gpgpu.js"></script>
<script src="../lib/gpugaussian.js"></script>
<script src="../lib/gpurand.js"></script>
<script id="draw-input" type="x-shader/x-fragment">
void main() {
    float cx = mod(floor(gl_FragCoord.x/64.),2.);
    float cy = mod(floor(gl_FragCoord.y/64.),2.);
    float chex = mod(cx+cy,2.);
    gl_FragColor = vec4(chex,chex,chex,1);
}
</script>
<script id="wc" type="x-shader/x-fragment">
uniform sampler2D Uin;
uniform sampler2D Uconv;
uniform sampler2D Unoise;
#define dt 0.1
#define Te 5.0
#define Ti 25.0
#define DE (dt/Te)
#define DI (dt/Ti)

// firing nonlinearity
float F(float x) {
    return 1./(1.+exp(-x));
}
vec2 EncodeFloatXY( float v ) {
  vec2 enc = vec2(1.0, 255.0) * v;
  enc = fract(enc);
  enc -= enc.yy * vec2(1./255.,0.);
  return enc;
}
float DecodeFloatXY( vec2 xy ) {
  return dot( xy, vec2(1.,1./255.) );
}
void main() {
    float Aei = 16.5;
    float Aii = 13.;
    float Aee = 16.;
    float Aie = 12.;
    float Hi  = 3.0;
    float He  = 3.5;
    vec2 XY = gl_FragCoord.xy/vec2(W,H);
    vec4 U  = texture2D(Uin   ,XY);
    vec4 Uc = texture2D(Uconv ,XY);
    vec4 N  = texture2D(Unoise,XY);
    float Ue  = DecodeFloatXY(U.rg);
    float Ui  = DecodeFloatXY(U.ba);
    float Uec = DecodeFloatXY(Uc.rg);
    float Uic = DecodeFloatXY(Uc.ba);
	float Ve = F(Aee*Uec-Aei*Uic-He+N.r*2.5+3.);
	float Vi = F(Aie*Uec-Aii*Uic-Hi+N.b*2.5+3.);
	Ue += (Ve-Ue)*DE;
	Ui += (Vi-Ui)*DI;
    gl_FragColor = vec4(EncodeFloatXY(Ue),EncodeFloatXY(Ui));
}
</script>
<script id="show" type="x-shader/x-fragment">
uniform sampler2D data;
// float is still clamped between 0 and 1, but we use the extra
// byte to get a little more precision.
float DecodeFloatXY( vec2 xy ) {
  return dot( xy, vec2(1.,1./255.) );
}
void main() {
    vec2 XY = gl_FragCoord.xy/vec2(W,H);
    vec4 U  = texture2D(data,XY);
    float Ue  = DecodeFloatXY(U.rg);
    float Ui  = DecodeFloatXY(U.ba);
    gl_FragColor = vec4(Ue,Ui,abs(Ue-Ui)*3.0,1.);
}
</script>
<script>

// This is the main script that will run when the website loads
function main()
{
    // Retrieve a handle to the canvas element
    var canvas = $("maincanvas");
    // Create a WebGL context on the canvas, abort if fail
    var gl = getRasterGL(canvas);
    if (!gl) OUT;

    var wc     = getRasterProgram(gl,'wc');
    var show   = getRasterProgram(gl,'show');
    var copy   = GPUcopy(gl);
    var blur   = GPUGaussianBlur(gl,1.0);
    var noise  = GPUNoise(gl);

    var temp = newBasicFramebuffer(gl,{wrap : gl.REPEAT});
    var noise_buffer = newBasicFramebuffer(gl,{wrap : gl.REPEAT});
    var U  = newBasicFramebuffer(gl,{wrap : gl.REPEAT});
    var UC = newBasicFramebuffer(gl,{wrap : gl.REPEAT});

    noise.randomize( noise_buffer );
    noise.randomize( U );

    function animate() {
        setTimeout(function(){
            for (var ii=0; ii<5; ii++) {
                noise(noise_buffer,temp);
                blur(U,temp,UC);
                wc({Uin:U,
                    Uconv:UC,
                    Unoise:noise_buffer,
                    },temp);
                copy(temp,U);
            }
            show({data:U});
            requestAnimationFrame(animate);
        },1000./100.);
    }
    animate();
}

</script>
<body onload="javascript:main()">
<canvas id='maincanvas' style="width:512px;height:512px;"></canvas>
</body>
</html>
